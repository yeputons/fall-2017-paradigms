\section{Классы типов}
\subsection{Что и зачем}

\begin{frame}
	\tableofcontents[currentsection,currentsubsection]
\end{frame}

\begin{frame}[fragile]{Pattern Matching и \t{==}}
\begin{minted}{haskell}
data IntList = Empty | Cons Int IntList deriving Show
a = Cons 1 (Cons 2 Empty)
b = Cons 1 (Cons 2 Empty)
c = Cons 1 (Cons 3 Empty)

isA :: IntList -> Bool
isA (Cons 1 (Cons 2 Empty)) = True
isA _                       = False

isA a -- True
isA b -- True
isA c -- False
a == b  -- ошибка компиляции?
a == c  -- ошибка компиляции?
\end{minted}
\end{frame}

\begin{frame}[fragile]{Eq}
	\begin{itemize}
		\item Pattern Matching "--- конструкция на уровне языка.
		\item \t{==} "--- просто некоторая функция с таким названием.
		\item В C++ мы бы написали перегрузку функции/оператора.
		\item В Haskell пишем так:
\begin{minted}{haskell}
instance Eq IntList where
  Empty       == Empty       = True
  (Cons x xs) == (Cons y ys) = (x == y) && (xs == ys)
  _           == _           = False

a == b  -- True
a == c  -- False
b == c  -- False
Empty == Empty           -- True
Empty /= (Cons 1 Empty)  -- True, /= тоже работает
\end{minted}
	\end{itemize}
\end{frame}

\begin{frame}[fragile]{class Eq}
	\begin{itemize}
		\item \t{Eq} "--- это \textit{класс типов}, который описывает, что к типам можно применять определённые функции:
\begin{minted}{haskell}
class Eq a where
  (==) :: a -> a -> Bool
  (/=) :: a -> a -> Bool
\end{minted}
		\item Говорим, что тип \t{a} лежит в классе \t{Eq} тогда и только тогда, когда для него есть функции \t{(==)} и \t{(/=)}
		\item Класс типов "--- это такой <<интерфейс>> для типов.
		\item Некоторые функции требуют, чтобы параметры были в определённых классах:
\begin{minted}{haskell}
lookup :: Eq a => a -> [(a, b)] -> Maybe b
\end{minted}
		\item Слово \t{instance} на предыдущем слайде добавляло \t{IntList} в класс \t{Eq}.
		\item Не путать с классами объектов из ООП!
	\end{itemize}
\end{frame}

\begin{frame}[fragile]{Реализации по умолчанию}
	\begin{itemize}
		\item Для \t{IntList} мы реализовали только \t{==}, а \t{/=} получили автоматом.
		\item В классе можно указывать реализацию по умолчанию:
\begin{minted}{haskell}
class Eq a where
  (==) :: a -> a -> Bool
  (==) a b = not (a /= b)
  (/=) :: a -> a -> Bool
  (/=) a b = not (a == b)
\end{minted}
		\item Тогда \textit{минимальное полное определение} для \t{Eq} "--- это либо \t{==}, либо \t{/=}.
	\end{itemize}
\end{frame}

\subsection{Для параметризованных типов}
\begin{frame}[fragile]{Класс Eq для списков}
	\begin{itemize}
		\item Пусть есть свой класс для списков:
\begin{minted}{haskell}
data List a = Empty | Cons a (List a)
\end{minted}
		\item Разумно считать, что списки равны, если равны элементы:
\begin{minted}{haskell}
instance Eq (List a) where
  Empty       == Empty       = True
  (Cons x xs) == (Cons y ys) = (x == y) && (xs == ys)
  _ == _                     = False
\end{minted}
		\item Не скомпилируется, потому что элементы произвольного типа \t{a} нельзя сранивать.
		\item Надо добавить \textit{контекст} "--- сказать, что списки можно сравнивать только если можно сранивать элементы:
\begin{minted}{haskell}
instance Eq a => Eq (List a) where
\end{minted}
	\end{itemize}
\end{frame}

\begin{frame}[fragile]{Классы для структур данных}
	\begin{itemize}
		\item Иногда хочется указать, что структура данных обладает некоторым свойством:
\begin{minted}{haskell}
class Mappable f where
  map' :: (a -> b) -> f a -> f b
\end{minted}
		\item \t{Mappable} "--- что-то, содержащее элементы произвольного типа, к чему можно делать \t{map'}.
		\item Можно реализовать:
\begin{minted}{haskell}
instance Mappable List where
  map' _ Empty = Empty
  map' f (Cons x xs) = Cons (f x) (map' f xs)

map' (+1) (Cons 1 (Cons 10 Empty))
-- Cons 2 (Cons 11 Empty)
\end{minted}
	\end{itemize}
\end{frame}

\begin{frame}[fragile]{Functor}
	\begin{itemize}
		\item Аналог \t{Mappable} в Haskell называется \t{Functor}, в нём определена функция \t{fmap}.
		\item \t{fmap} должна удовлетворять некоторым аксиомам, но их компилятор сам не проверит:
\begin{minted}{haskell}
fmap id x == x
fmap (f . g) x == fmap f (fmap g x)
\end{minted}
		\item После реализации \t{Functor} наш новый тип можно использовать в том числе в старых функциях:
\begin{minted}{haskell}
a = (Cons 1 (Cons 2 (Cons 3 Empty)))
fmap (+1) [1,2,3]
fmap (+1) a
-- Встроенный оператор замены значений на константу
10 <$ [1,2,3]  -- [10,10,10]
10 <$ a
\end{minted}
	\end{itemize}
\end{frame}
