\subsection{Примеры типов-сумм}
\begin{frame}
	\tableofcontents[currentsection,currentsubsection]
\end{frame}

\begin{frame}[fragile]{CharOrNotFound}
	Поиск элемента по номеру:
\begin{minted}{haskell}
data CharOrNotFound = NotFound | Found Char deriving Show

getItem :: [Char] -> Int -> CharOrNotFound
getItem (x:_ ) 0         = Found x
getItem (x:xs) n | n > 0 = getItem xs (n - 1)
getItem _      _         = NotFound
\end{minted}
	\begin{itemize}
		\item Не требуются <<магические значения>> для ситуации <<элемент не найден>>.
		\item Компилятор проверяют, что мы всегда обрабатываем оба случая.
		\item По типу функции сразу понятно, что она может вернуть.
		\item Нет исключений; функции чистые.
	\end{itemize}	
\end{frame}


\begin{frame}[fragile]{Maybe}
	Можно обобщить до \textit{параметризованного типа}:
\begin{minted}{haskell}
data GetResult a = NotFound | Found a deriving Show

getItem :: [a] -> Int -> GetResult a
getItem (x:_ ) 0         = Found x
getItem (x:xs) n | n > 0 = getItem xs (n - 1)
getItem _ _              = NotFound
\end{minted}
% Показать :t getItem, :t getItem "123" 1
	\begin{itemize}
		\item \t{GetResult} "--- это не тип, это \textit{конструктор типа}.
		\item \t{a} "--- единственный параметр этого конструктора.
		\item А вот \t{GetResult Char} "--- уже конкретный тип:
\begin{minted}{haskell}
data GetResult Char = NotFound | Found Char
\end{minted}
		\item В Haskell такой тип называется (\t{Maybe}.
		\item А в Java есть generic-тип \t{Optional<>}).
		\item На самом деле \t{[Int]} "--- это сахар для \t{[] Int}.
	\end{itemize}
\end{frame}

\begin{frame}[t,fragile]{Упражнение}
	\begin{itemize}
		\item Напишите тип для функции \t{getItem}, если бы она использовала \t{Maybe}:
\begin{minted}{haskell}
-- Уже объявлен в языке, писать не надо.
data Maybe a = Nothing | Just a

getItem :: [a] -> Int -> ???
\end{minted}
		\item Напишите функцию \t{getItem}.
		\item Удалите явное указание типа, проверьте, какой тип вывелcя автоматически (\t{:t getItem} в GHCI).
	\end{itemize}
	\pause
\begin{minted}{haskell}
getItem :: [a] -> Int -> Maybe a
getItem (x:_)  0         = Just x
getItem (x:xs) n | n > 0 = getItem xs (n - 1)
getItem _ _              = Nothing
\end{minted}
\end{frame}

\begin{frame}[fragile]{Either}
	На случай, если хотим сообщить об ошибке:
\begin{minted}{haskell}
-- Уже объявлен в языке, писать не надо.
data Either a b = Left a | Right b

parseBool :: String -> Either String Bool
parseBool "true"  = Right True
parseBool "false" = Right False
parseBool x       = Left ("Invalid value: " ++ x)
\end{minted}
	\begin{itemize}
		\item Обычно за \t{Right} принимает успешное вычисление (<<правильный>> результат).
		\item А за \t{Left} "--- сообщение об ошибке (<<левый>> результат).
	\end{itemize}
\end{frame}

\begin{frame}[fragile]{Двоичная куча}
\begin{minted}{haskell}
data Heap = Nil | Node Int Heap Heap deriving Show

Node 1 (Node 2 (Node 5 (Node 6 Nil Nil) Nil)
               (Node 4 Nil Nil))
       (Node 3 Nil Nil)
\end{minted}
	\begin{center}
		\begin{dot2tex}[scale=0.5,options=-tmath]
			graph G {
			    1 {rank=same 2 3} {rank=same 5 4} { rank=same 6 x }
			    1 -- {2 3};
			    2 -- {5 4};
			    5 -- 6;
			    5 -- x [style=invis];
			    x [style=invis];
			}
		\end{dot2tex}
	\end{center}
\end{frame}

\begin{frame}[fragile]{Min-Max куча}
	Так как вершина всегда хранит либо минимум, либо максимум, это можно указать прямо в типе.
	Тогда точно не запутаемся, где какая вершина, и не надо это явно считать и передавать.
\begin{minted}{haskell}
data MinMaxHeap = Nil | MinNode Int MinMaxHeap MinMaxHeap
                      | MaxNode Int MinMaxHeap MinMaxHeap
\end{minted}
	Можно даже строже, у нас есть взаимная рекурсия:
\begin{minted}{haskell}
data MinMaxHeap = Nil1 | MinNode Int MaxMinHeap MaxMinHeap
data MaxMinHeap = Nil2 | MaxNode Int MinMaxHeap MinMaxHeap
\end{minted}	
	К сожалению, назвать оба конструктора данных \t{Nil} нельзя.
\end{frame}

\begin{frame}[fragile]{Дерево разбора выражения}
\begin{minted}{haskell}
data BinOp = Add | Sub | Mul | Div deriving Show
data Tree = Number Int
          | Reference String
          | BinaryOperation BinOp Tree Tree
          deriving Show
-- Глубокий pattern matching
fold' Sub (Reference a) (Reference b) | a == b = Number 0
fold' Mul (Reference _) (Number 0)             = Number 0
fold' Mul (Number 0)    (Reference _)          = Number 0
fold' op a b = BinaryOperation op a b
-- Рекурсивное сворачивание выражений
fold (BinaryOperation op a b) =
    fold' op (fold a) (fold b)
fold x = x
\end{minted}
\end{frame}

\begin{frame}[fragile]{Односвязные списки}
\begin{minted}{haskell}
data List a = Empty | Cons a (List a) deriving Show

head' (Cons x _ ) = x
tail' (Cons _ xs) = xs
\end{minted}
	\begin{itemize}
		\item Выше написано почти определение встроенного списка.
		\item \t{[]} "--- это сахар для конструктора \t{Empty}.
		\item \t{:} "--- это сахар для конструктора \t{Cons}.
		\item Конкретно в Haskell любые структуры бывают бесконечными из-за ленивости, не только списки.
		\item Например, бесконечное двоичное дерево имеет право на жизнь.
	\end{itemize}
\end{frame}

\begin{frame}[fragile]{Упражнение}
	\begin{enumerate}
		\item Напишите тип <<двоичное дерево>> (\t{Tree}), в котором у каждой вершины либо 0 детей, либо 2, а каждая вершина содержит значение типа \t{Int}.
		\item Добавьте \t{deriving Show}.
		\item Напишите функцию \t{tree\_sum}, которая считает сумму в данном ей дереве.
		\item Удалите разбор какого-нибудь случая и запустите GHCI так: \t{ghci~-W~file.hs}
		\item Убедитесь, что выпало предупреждение о неразобранном случае.
		\item Напишите функцию, которая возвращает бесконечное дерево \t{Int}'ов, где каждая вершина содержит номер своего уровня.
		\item Выведите результат на экран, объясните увиденное.
	\end{enumerate}
\end{frame}

\begin{frame}{Промежуточные итоги}
	\begin{itemize}
		\item
			Под <<алгебраическими типами данных>> обычно подразумевают поддержку типов-сумм вместе с типами-произведениями \textit{на уровне языка}.
			Такая поддержка даёт:
			\begin{enumerate}
				\item Более наглядные типы.
				\item Невозможность обратиться к данным из другого <<случая>>.
				\item Pattern matching и сильное упрощение кода.
				\item Предупреждения компилятора о нерассмотренных случаях (ключ \t{-W} для GHC/GHCI).
			\end{enumerate}
		\item Добавлять случаи в тип-сумму обычно после объявления нельзя.
		\item В языках без типов-сумм, но с ООП, обычно используется:
			\begin{itemize}
				\item Наследование от общего предка вместо типов-сумм.
				\item Visitor вместо pattern mactching.
			\end{itemize}
		\item Типы-суммы очень часто возникают при работе с AST.
		\item В Haskell любой пользовательский тип является типом-суммой (возможно, из одного слагаемого).
		\item В Haskell можно параметризовать пользовательские типы.
	\end{itemize}
\end{frame}
